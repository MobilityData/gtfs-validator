/*
 * Copyright 2020-2022 Google LLC, MobilityData IO
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'application'
    id 'java'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

mainClassName = 'org.mobilitydata.gtfsvalidator.app.gui.Main'

dependencies {
    implementation project(':core')
    implementation project(':main')
    implementation 'com.google.flogger:flogger:0.6'
    implementation 'com.google.flogger:flogger-system-backend:0.6'
    testImplementation group: 'junit', name: 'junit', version: '4.13'
    testImplementation 'com.google.truth:truth:1.0.1'
    testImplementation 'com.google.truth.extensions:truth-java8-extension:1.0.1'
    testImplementation 'org.mockito:mockito-core:4.5.1'
}

jar {
    manifest {
        attributes('Implementation-Title': rootProject.name,
                'Implementation-Version': project.version,
                'Main-Class': 'org.mobilitydata.gtfsvalidator.app.gui.Main')
    }
}

shadowJar {
    minimize {
        // We don't want to minimize the :main project, as it will drop the validators
        // loaded via reflection
        exclude(project(':main'))

        exclude(dependency('org.apache.httpcomponents:httpclient'))
    }

    // Some of our dependencies include their own module-info declarations.  We drop
    // all of them, as we'll be wrapping the entire uber jar as a module in :app:pkg
    // and we don't want these existing module-info declarations to conflict.
    exclude 'module-info.class'
    exclude 'META-INF/versions/*/module-info.class'
}
