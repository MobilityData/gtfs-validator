/*
 * Copyright 2020 Google LLC, MobilityData IO
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'application'
    id 'java'
}

applicationDefaultJvmArgs = [ "-Xmx8G" ]

jar {
    // Add the manifest within the JAR, using gtfs-validator as the title
    manifest {
        attributes('Implementation-Title': rootProject.name,
                'Implementation-Version': project.version,
                'Main-Class': 'org.mobilitydata.gtfsvalidator.cli.Main',
                'Multi-Release': 'true')
    }
}

shadowJar {
    minimize {
        exclude(dependency('org.apache.httpcomponents:httpclient'))
    }
    // Change the JAR name from 'main' to 'gtfs-validator'
    archiveBaseName = rootProject.name
    // Change the JAR suffix from 'all' to 'cli'
    archiveClassifier = 'cli'
}

sourceCompatibility = JavaVersion.VERSION_11
mainClassName = 'org.mobilitydata.gtfsvalidator.cli.Main'
compileJava.options.encoding = "UTF-8"

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':core')
    implementation project(':main')
    implementation 'com.beust:jcommander:1.48'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.google.flogger:flogger:0.6'
    implementation 'com.google.flogger:flogger-system-backend:0.6'
    implementation 'com.google.guava:guava:31.0.1-jre'

    testImplementation group: 'junit', name: 'junit', version: '4.13'
    testImplementation 'com.google.truth:truth:1.0.1'
    testImplementation 'com.google.truth.extensions:truth-java8-extension:1.0.1'
    testImplementation 'org.mockito:mockito-core:4.5.1'
}

test {
    // Always run tests, even when nothing changed.
    dependsOn 'cleanTest'

    // Show test results.
    testLogging {
        events "passed", "skipped", "failed"
    }

    systemProperty 'gtfsValidatorVersionForTest', project.version
}

// Share the test report data to be aggregated for the whole project
configurations {
    binaryTestResultsElements {
        canBeResolved = false
        canBeConsumed = true
        attributes {
            attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category, Category.DOCUMENTATION))
            attribute(DocsType.DOCS_TYPE_ATTRIBUTE, objects.named(DocsType, 'test-report-data'))
        }
        outgoing.artifact(test.binaryResultsDirectory)
    }
}
