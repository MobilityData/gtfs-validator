plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'application'
    id 'java'
}

group 'org.mobilitydata'
version "${System.getenv("versionTag")}"

group 'org.mobilitydata'
version "${System.getenv("versionTag")}_cli"

jar {
    // Add the manifest within the JAR, using gtfs-validator as the title
    manifest {
        attributes('Implementation-Title': rootProject.name,
                'Implementation-Version': project.version,
                'Main-Class': 'org.mobilitydata.gtfsvalidator.cli.Main',
                'Multi-Release': 'true')
    }
}

shadowJar {
    minimize {
        exclude(dependency('org.apache.logging.log4j:log4j-core'))
        exclude(dependency('com.fasterxml.jackson.core:jackson-databind'))
        exclude(dependency('org.apache.httpcomponents:httpclient'))
    }
    // Change the JAR name from cli-app to gtfs-validator
    archiveBaseName = rootProject.name
    // Remove "-all" from the end of the JAR file name
    archiveClassifier = null
    // Add "v" to version to match previous releases
    archiveVersion = project.version
}

sourceCompatibility = JavaVersion.VERSION_1_8
mainClassName = 'org.mobilitydata.gtfsvalidator.cli.Main'

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor project(':processor')
    compileOnly project(':processor')
    implementation project(':core')
    implementation 'org.apache.logging.log4j:log4j-api:2.12.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.12.1'
    implementation 'com.beust:jcommander:1.48'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.10.0'
    implementation 'com.google.guava:guava:29.0-jre'
    testImplementation group: 'junit', name: 'junit', version: '4.13'
    testImplementation "com.google.truth:truth:1.0.1"
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.10.0'
    testImplementation 'org.mockito:mockito-core:1.10.19'
}

test {
    // Always run tests, even when nothing changed.
    dependsOn 'cleanTest'

    // Show test results.
    testLogging {
        events "passed", "skipped", "failed"
    }
}

